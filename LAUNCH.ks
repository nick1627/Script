//Launch.
PARAMETER ORBALT.
parameter Inclination is 0.

PRINT "LAUNCH SEQUENCE INITIATED.".
PRINT "TARGET ALTITUDE: " + ORBALT + " METRES".
print "TARGET INCLINATION:  " + Inclination + " DEGREES".

//Pre flight procedures.
SAS OFF.
LOCK THROTTLE TO 1.0.

//start countdown.
PRINT "COUNTING DOWN...".
PRINT "T - 5".
WAIT 1.
FROM {LOCAL COUNT IS 4.} UNTIL COUNT = 0 STEP {SET COUNT TO COUNT -1.} DO {
	PRINT COUNT.
	WAIT 1.
}

//Launch
LOCK STEERING TO HEADING(0, 90).
STAGE.
PRINT "WE HAVE LIFTOFF".
WAIT 5.
LOCK STEERING TO HEADING(90 - Inclination, 90).
WAIT 1.

//ascend to 1000m
UNTIL SHIP:ALTITUDE > 1000{
  IF CHECKSTAGEREQUIRED{
		PRINT "STAGING.".
		STAGE.
	}
}

//start gravity turn
PRINT "COMMENCING GRAVITY TURN".

LOCAL PITCH IS 90.
LOCK STEERING TO HEADING(90 - Inclination, PITCH).
Wait 1.
UNTIL SHIP:ALTITUDE > 10000 {
	SET PITCH TO ((-0.005*(SHIP:ALTITUDE)) + 95).
	
  	IF CHECKSTAGEREQUIRED{
			PRINT "STAGING.".
			STAGE.
	}
}

LOCK STEERING TO HEADING(90 - Inclination, 45).
PRINT "GRAVITY TURN COMPLETE".


UNTIL SHIP:APOAPSIS >= ORBALT{
    IF CHECKSTAGEREQUIRED{
		PRINT "STAGING.".
		STAGE.
	}
}

LOCK THROTTLE TO 0.0.

PRINT "TARGET APOAPSIS REACHED".

PRINT "COASTING . . .".

UNLOCK STEERING.

SAS ON.

WAIT 1.

SET SASMODE TO "PROGRADE". //but what if you dont have sas modes available?  need to use a vector really.

WAIT UNTIL SHIP:ALTITUDE>70000.

SAS OFF.

PRINT "CALCULATING CIRCULARISATION BURN".
//calculate target velocity (speed at apoapsis)
SET TARGETVELOCITY TO ((CONSTANT:G*KERBIN:MASS)/(600000+SHIP:APOAPSIS))^0.5.
//Now calculate speed at apoapsis
//calculate current orbit energy
LOCAL E IS (0.5*(SHIP:VELOCITY:ORBIT:MAG)^2)-(CONSTANT:G * KERBIN:MASS/(SHIP:ALTITUDE+KERBIN:RADIUS)).
//calculate velocity at current apoapsis before burning
LOCAL APOAPSISVELOCITY IS VELOCITYAT(SHIP,(TIME + ETA:APOAPSIS)):ORBIT:MAG.
//calculate change in velocity required.
LOCAL DELTAV IS TARGETVELOCITY - APOAPSISVELOCITY.


SET CIRCMANOEUVRE TO NODE(TIME:SECONDS + ETA:APOAPSIS, 0, 0, DELTAV).
ADD CIRCMANOEUVRE.

EXECUTEMANOEUVRE(CIRCMANOEUVRE).

WAIT 1.

//check and change manoeuvre here in case it went wrong



REMOVE CIRCMANOEUVRE.

UNLOCK THROTTLE.

SET THROTTLE TO 0.0.
SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 0.

PRINT "SECO".

UNLOCK STEERING.

IF SHIP:PERIAPSIS > 70000{
	PRINT "ORBIT REACHED.".
	PRINT "WARNING:  EXTEND THE PANELS AND COMMS ARRAY.".
}ELSE{
	PRINT "ERROR!  BURN FINISHED BEFORE STABLE ORBIT REACHED.".
	PRINT "ALLOWING MANUAL CONTROL.  SET MANOEUVRES.".
	MONITORFORMANOEUVRES(10).
	IF SHIP:PERIAPSIS < 70000{
		PRINT "ABORT".
		ABORTNOW().
	}
}
